rule Multiple_Failed_Logins_By_Single_User_Over_Multiple_Destinations {
  meta:
    author = "Jonathan Heng"
    description = "Detects multiple failed logins by a single user over multiple destinations"
    date = "2025-06-01"
    severity = "LOW"
    priority = "LOW"
    platform = "Windows"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force"
    version = "1.00"
    
  events:
    $fail.metadata.event_type = "USER_LOGIN"
    $fail.metadata.vendor_name = "Microsoft"
    $fail.target.user.userid = $targetUser
    $fail.target.ip  = $targetHost
    $fail.security_result.action = "BLOCK"
    $fail.target.user.userid != /^.*\$/

  match:
    $targetUser over 10m

  outcome:
    $target_host_distinct_count = count_distinct($targetHost)
    $target_hosts_uniq_list = array_distinct($targetHost)

  condition:
    #fail > 4 and $target_host_distinct_count > 1
}
